
# BUGS model specification begins ...

model {

  for( i in 1 : N ) {
     y[i]   ~  dnorm(mu[i], tau[i])  # likelihood
     mu[i]  <- lambda[G[i]]          # prior for mean
     tau[i] <- lambdaTau[G[i]]       # prior for precision
     G[i]   ~  dcat(P[])             # the cluster attributions for each y_i
  }   

  P[1:2] ~ ddirch(alpha[])           # dirichlet distribution (in this case just for 2 clusters)
  alpha[1] <- 0.5                    # It generalizes the beta (with K=2 we could have used the beta), and
  alpha[2] <- 0.5                    # is the conjugate for the categorical distribution

  lambda[1] ~ dnorm(0.0, 1.0E-6)     # hyperparameters for mean
  lambda[2] <- lambda[1] + theta
  theta ~ dnorm(0.0, 1.0E-6)I(0.0, )

  lambdaTau[1] ~ dgamma(0.01,0.01)   # hyperparameters for precision/standard deviation
  lambdaTau[2] ~ dgamma(0.01,0.01)

  sigma[1] <- 1 / sqrt(lambdaTau[1])
  sigma[2] <- 1 / sqrt(lambdaTau[2])
}

# ... BUGS model specification ends.

